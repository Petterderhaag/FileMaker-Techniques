/**
 * =====================================
 * AuditCommitID ( logData ; sessionKey )
 *
 * RETURNS:
 *		The event ID (UUID) from the first (most recent) line of
 *		logData.
 *
 * PARAMETERS:
 *		logData: A series of log entries, as generated by the AuditLog
 *			function.
 *		sessionKey: The session key from the appropriate session row in the log
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-06-12 by Jeremy Bante <http://scr.im/jbante>.
 * =====================================
 */

Let ( [
	~TAB = "	";
	~log = GetValue ( logData ; 1 );
	~log = Substitute ( ~log ; ~TAB ; "¶" );
	~timestamp = GetAsTimestamp ( GetValue ( ~log ; 1 ) )
];
	"1-2-" // version and reserved variant code
	& Right ( "000000000000" & Int ( ~timestamp ) ; 12 )
	& "-"
	& Left (
		Substitute (
			Mod ( ~timestamp ; 1 );
			"."; ""
		)
		& "0000000";
		7
	)
	& "-"
	& sessionKey
)