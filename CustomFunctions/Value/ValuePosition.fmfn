/*******************************************************************************
 * ValuePosition ( valueList ; searchValue ; start ; occurrence )
 * Implements a version of FileMaker's built-in Position function to operate on
 * values in return-delimited lists instead of characters in text strings.
 *
 * @parameter valueList: A return-delimited list of values to search in
 * @parameter searchValue: A value to search for
 * @parameter start: The value within valueList to begin searching at
 * @parameter occurrence: The occurrence of the value to find
 *
 * @return The value number of searchValue in valueList
 *
 * @history 2011-05-22 - Jeremy Bante <http://scr.im/jbante> - Created
 * @history 2014-06-24 - Jeremy Bante <http://scr.im/jbante> - Full feature
 * parity with built-in Position function.
 * @history 2015-03-18 - Jeremy Bante <http://scr.im/jbante> - Improving
 * performance when the value is not found by only using Left on the valueList
 * when the value is found (based on an example by Will Baker’s mustache
 * (https://beezwax.net/beez/100)).
 ******************************************************************************/

Let ( [
	_valueList = ¶ & Substitute ( valueList ; ¶ ; "¶¶" ) & ¶ ;
	_startPosition = Position ( _valueList ; ¶ ; 1 ; start * 2 - 1 ) ;
	_position =
		Position (
			_valueList ;
			¶ & searchValue & ¶ ;
			_startPosition ;
			occurrence
		)
] ;
	If ( _position = 0 ;	// value was not found
		0 ;
	/* Else */
		( PatternCount ( Left ( _valueList ; _position ) ; ¶ ) - 1 ) / 2 + 1
	)
)