/**
 * =====================================
 * NavBackLayoutName
 *
 * PURPOSE:
 *		Logs the current layout in stacks of previous layouts to support Web
 *		browser-style "back" and "forward" navigation between FileMaker layouts.
 *		This custom function is designed to be used in a "back" button.
 *
 * RETURNS:
 *		The layout name of the next layout in the "backwards" navigation
 *		direction.
 *
 * PARAMETERS: none
 *
 * DEPENDENCIES: none
 *
 * VARIABLES:
 *		$$~NAV.BACKLAYOUTS:	A list of layouts for navigating "backwards", with
 *			the current layout on top.
 *		$$~NAV.FORWARDLAYOUTS:	A list of layouts for navigating "forwards",
 *			with the current layout on top.
 *
 * NOTES:
 *		This function is designed to work in a single-window solution. It will
 *		need to be modified to work appropriately in a multi-window application.
 *
 * HISTORY:
 *		MODIFIED on 2012-01-02 by Jeremy Bante to not modify variables when
 *		there isn't actually a target layout.
 *		CREATED on 2011-12-30 by Jeremy Bante <http://scr.im/jbante>.
 * =====================================
 */

Let ( [
	~previous = GetValue ( $$~NAV.BACKLAYOUTS ; 2 );

	$$~NAV.BACKLAYOUTS =
		If ( IsEmpty ( ~previous );
			$$~NAV.BACKLAYOUTS;
			/* Else */ RightValues (
				$$~NAV.BACKLAYOUTS;
				ValueCount ( $$~NAV.BACKLAYOUTS ) - 1
			)
		);
	$$~NAV.FORWARDLAYOUTS =
		If ( IsEmpty ( ~previous );
			$$~NAV.FORWARDLAYOUTS;
			/* Else */ List ( ~previous ; $$~NAV.FORWARDLAYOUTS )
		)
];
	~previous
)