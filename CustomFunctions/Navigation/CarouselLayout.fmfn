/**
 * =====================================
 * CarouselLayout ( index )
 *		https://github.com/jbante/FileMaker-Techniques/blob/master/CustomFunctions/Carousel/CarouselLayout.fmfn
 *
 * PURPOSE:
 *		Finds the layout that would have the given layout number if hidden
 *		layouts and separators were omitted from the list of all layouts in the
 *		current file. This can be used to construct an index for files using
 *		carousel navigation. This navigation pattern can be particularly useful
 *		in files used as presentation devices, such as technique demonstration
 *		files.
 *
 * PARAMETERS:
 *		index: The logical layout number of the layout name to retrieve
 *
 * RETURNS:
 *		The name of the layout indicated by the logical index.
 *
 * DEPENDENCIES:
 *		This function requires developers to adhere to a layout naming
 *		convention for correct operation. Layouts that will be omitted from the
 *		normal sequence for a file must be prefixed with a comment syntax:
 *		-	- (separator layouts)
 *		-	// (C++ style comments)
 *		-	# (FileMaker script style comments)
 *		-	~ (private objects according to FileMakerStandards.org)
 *
 * HISTORY:
 *		MODIFIED on 2011-10-24 by Jeremy Bante to accept the tilde "~" as a
 *		comment prefix.
 *		MODIFIED on 2011-01-25 by Jeremy Bante to improve error handling.
 *		CREATED on 2011-01-20 by Jeremy Bante <http://scr.im/fugue>.
 * =====================================
 */

Case (
	/* Step 0, initialize processing */
	not $~step;
	Let ( [
		$~layoutNames = LayoutNames ( Get ( FileName ) );
		$~layoutCount = ValueCount ( $~layoutNames );
		$~step =
			If (	//index is out of bounds
				index < 1
				or index > $~layoutCount;
					3;	//skip to returning empty (error)

				/* Else */ 1
			)
	];
		CarouselLayout ( Int ( index ) )
	);

	/* Step 1, find layout name by index */
	$~step = 1;
	Let ( [
		$~i = $~i + 1;
		$~layout = GetValue ( $~layoutNames ; $~i );
		~prefix1 = Left ( $~layout ; 1 );
		~prefix2 = Left ( $~layout ; 2 );
		~isHidden =
			~prefix1 = "-"	// separator layout
			or ~prefix1 = "~"	// "private" entity convention
			or ~prefix1 = "#"	// script or Python comment syntax
			or ~prefix2 = "//";	// calculation or C++ comment syntax
		$~index = If ( ~isHidden ; $~index ; /* Else */ $~index + 1 );
		$~step =
			Case (
				$~index = index ; 3;	// return result
				$~i = $~layoutCount ; 2;	// error, layout not found
				/* Else */ $~step
			)
	];
		CarouselLayout ( index )
	);

	/* Step 2, clear $~layout in event that correct layout can't be found */
	$~step = 2;
	Let ( [
		$~layout = "";
		$~step = $~step + 1
	];
		CarouselLayout ( index )
	);

	/* Step 3, clean-up */
	$~step = 3;
	Let ( [
		~layout = $~layout;

		// purge variables
		$~i = "";
		$~index = "";
		$~layoutCount = "";
		$~layoutNames = "";
		$~layout = "";
		$~step = ""
	];
		~layout
	)
)