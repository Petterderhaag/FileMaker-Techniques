/*******************************************************************************
 * FieldID ( file ; field )
 *
 * @parameter file: The name of the FileMaker file containing the field
 * @parameter field: The field (not literal field name) to retrieve the ID of
 *
 * @return FileMaker's internal ID for a field.
 *
 * @history 2012-01-26 - Jeremy Bante <http://scr.im/fugue> - Adding file parameter.
 * @history 2011-06-01 - Jeremy Bante <http://scr.im/fugue> - Created. Inspired
 * by custom functions by Fabrice Nordman.
 ******************************************************************************/

Let ( [
	_fieldName = Substitute ( GetFieldName ( field ) ; "::" ; "¶" ) ;
	_tableName = GetValue ( _fieldName ; 1 ) ;
	_fieldName = GetValue ( _fieldName ; 2 ) ;
	_nameList = ¶ & FieldNames ( file ; _tableName ) & ¶ ;
	_position = Position ( _nameList ; ¶ & _fieldName & ¶ ; 1 ; 1 ) ;
	_value = PatternCount ( Left ( _nameList ; _position ) ; "¶" ) ;
	_id = GetValue ( FieldIDs ( file ; _tableName ) ; _value )
];
	If ( _position = 0 ; "?" ; /* Else */ _id )
)