/**
 * =====================================
 * LayoutID ( file ; theLayoutName )
 *
 * RETURNS:
 *		FileMaker's internal ID for a layout.
 *
 * PARAMETERS:
 *		file: The name of the FileMaker file containing the layout.
 *		theLayoutName
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		MODIFIED on 2012-01-26 by Jeremy Bante to accept the file parameter.
 *		CREATED on 2011-06-01 by Jeremy Bante <http://scr.im/jbante>.
 *		INSPIRED by custom functions by Fabrice Nordman.
 * =====================================
 */

Let ( [
	~nameList = ¶ & LayoutNames ( file ) & ¶;
	~value =
		Position ( ~nameList ; ¶ & theLayoutName & ¶ ; 1 ; 1 );
	~value = PatternCount ( Left ( ~nameList ; ~value ) ; ¶ )
];
	GetValue ( LayoutIDs ( file ) ; ~value )
)