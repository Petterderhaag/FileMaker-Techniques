/*******************************************************************************
 * LayoutID ( file ; theLayoutName )
 *
 * @parameter file: The name of the FileMaker file containing the layout
 * @parameter theLayoutName
 *
 * @return FileMaker's internal ID for a layout
 *
 * @history 2012-12-17 - Jeremy Bante <http://scr.im/fugue> - Using current
 * layout name if none is specified.
 * @history 2012-01-26 - Jeremy Bante <http://scr.im/fugue> - Adding file parameter
 * @history 2011-06-01 - Jeremy Bante <http://scr.im/fugue> - Created. Inspired
 * by custom functions by Fabrice Nordman.
 ******************************************************************************/

If ( IsEmpty ( theLayoutName ) ;
	GetValue ( LayoutIDs ( file ) ; Get ( LayoutNumber ) ) ;
/* Else */
	Let ( [
		_nameList = ¶ & LayoutNames ( file ) & ¶ ;
		_position = Position ( _nameList ; ¶ & theLayoutName & ¶ ; 1 ; 1 ) ;
		_value = PatternCount ( Left ( _nameList ; _position ) ; ¶ ) ;
		_id = GetValue ( LayoutIDs ( file ) ; _value )
	] ;
		If ( _position = 0 ; "?" ; /* Else */ _id )
	)
)