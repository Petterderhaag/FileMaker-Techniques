/*******************************************************************************
 * ScriptID ( file ; theScriptName )
 *
 * @parameter file: The name of the FileMaker file containing the script
 * @parameter theScriptName
 *
 * @return FileMaker's internal ID for a script
 *
 * @history 2017-02-10 - Jeremy Bante <http://scr.im/fugue> - Using different
 * error value "?".
 * @history 2012-12-17 - Jeremy Bante <http://scr.im/fugue> - Using the current
 * script name if none is specified.
 * @history 2012-02-20 - Jeremy Bante <http://scr.im/fugue> - Returning an error
 * value (-1) for unknown scripts.
 * @history 2012-01-26 - Jeremy Bante <http://scr.im/fugue> - Created. Inspired
 * by custom functions by Fabrice Nordman.
 ******************************************************************************/

Let ( [
	theScriptName =
		If ( IsEmpty ( theScriptName ) ;
			Get ( ScriptName ) ;
			/* Else */ theScriptName
		) ;
	_nameList = ¶ & ScriptNames ( file ) & ¶ ;
	_position = Position ( _nameList ; ¶ & theScriptName & ¶ ; 1 ; 1 ) ;
	_value = PatternCount ( Left ( _nameList ; _position ) ; ¶ ) ;
	_id = GetValue ( ScriptIDs ( file ) ; _value )
] ;
	If ( _position = 0 ; "?" ; /* Else */ _id )
)