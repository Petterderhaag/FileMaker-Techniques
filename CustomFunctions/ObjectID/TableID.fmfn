/*******************************************************************************
 * TableID ( file ; theTableName )
 *
 * @parameter file: The name of the FileMaker file containing the table
 * @parameter theTableName
 *
 * @return FileMaker's internal ID for a table
 *
 * @history 2012-12-17 - Jeremy Bante <http://scr.im/fugue> - Using the current
 * layout name if none is specified.
 * @history 2012-01-26 - Jeremy Bante <http://scr.im/fugue> - Adding file parameter.
 * @history 2011-06-01 - Jeremy Bante <http://scr.im/fugue> - Created. Inspired
 * by custom functions by Fabrice Nordman.
 ******************************************************************************/

Let ( [
	theTableName =
		If ( IsEmpty ( theTableName ) ;
			Get ( LayoutTableName ) ;
			/* Else */ theTableName
		) ;
	_nameList = ¶ & TableNames ( file ) & ¶ ;
	_position = Position ( _nameList ; ¶ & theTableName & ¶ ; 1 ; 1 ) ;
	_value = PatternCount ( Left ( _nameList ; _position ) ; ¶ ) ;
	_id = GetValue ( TableIDs ( file ) ; _value )
] ;
	If ( _position = 0 ; "?" ; /* Else */ _id )
)