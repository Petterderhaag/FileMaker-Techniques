/*******************************************************************************
 * RandomBinomial ( probability ; trials )
 * When called repeatedly, RandomBinomial returns values consistent with a
 * binomial random variable with probability of "event success" and number of
 * trials specified by the parameters.
 *
 * @parameter probability: a number, 0 ≤ probability ≤ 1
 * @parameter trials: a positive integer
 *
 * @return A number between 0 and trials
 *
 * @history 2010-02-22 - Jeremy Bante <http://scr.im/fugue> - Created
 *
 * @see http://en.wikipedia.org/wiki/Binomial_distribution
 ******************************************************************************/

Let ( [
	_trials = Abs ( Int ( trials ) ) ;
	_guess = Round ( Random * _trials ; 0 ) ;

	// mode of Binomial distribution
	_mode1 = Ceiling ( _trials * probability ) ;
	_mode2 = Floor ( _trials * probability ) ;

	// max probability of Binomial distribution
	_mode1Mass =
		( probability ^ _mode1 )
		* ( ( 1 - probability ) ^ ( _trials - _mode1 ) ) ;
	_mode2Mass =
		( probability ^ _mode2 )
		* ( ( 1 - probability ) ^ ( _trials - _mode2 ) ) ;

	_scaleFactor = 1 / Max ( _mode1Mass ; _mode2Mass ) ;
	_rejectionThreshold =	// probability mass at _guess
		( probability ^ _guess )
		* ( ( 1 - probability ) ^ ( _trials - _guess ) )
		* _scaleFactor	// maps function to [0,1] for max acceptance rate
] ;
	Case (
		0 > probability or 1 < probability ;	// parameter out of bounds
			"?" ;

		Random ≤ _rejectionThreshold ;
			_guess ;

		// Else, reject _guess and sample again
			RandomBinomial ( probability ; _trials )
	)
)