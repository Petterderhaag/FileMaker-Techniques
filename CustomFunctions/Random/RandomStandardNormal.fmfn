/*******************************************************************************
 * RandomStandardNormal
 * Returns a sample value from a normal ("Gaussian") distribution with mean 0
 * and standard deviation 1.
 *
 * @return a number
 *
 * @global $$~STANDARDNORMAL
 *
 * @history 2010-02-22 - Jeremy Bante <http://scr.im/jbante> - Created
 * @history 2011-02-22 - Jeremy Bante <http://scr.im/jbante> - Saving extra
 * value from Marsaglia polar method for later use.
 *
 * @see http://en.wikipedia.org/wiki/Normal_distribution
 ******************************************************************************/

If ( not IsEmpty ( $$~STANDARDNORMAL ) ;
	// Use extra value from previous call to this function
	$$~STANDARDNORMAL
	& Let ( $$~STANDARDNORMAL = "" ; "" ) ;

	// Else, find new value with Marsaglia polar sampling method
	Let( [
		_x = Random * 2 - 1 ;	// begin with random points
		_y = Random * 2 - 1 ;
		_radiusSquared = _x ^ 2 + _y ^ 2
	] ;

		Case (
			_radiusSquared < 1 ;	// (_x, _y) is inside the unit circle
				_x
				* sqrt ( -2 * ln ( _radiusSquared ) / _radiusSquared )
				& Let (
					$$~STANDARDNORMAL =	// save extra value
						_y
						* sqrt ( -2 * ln ( _radiusSquared ) / _radiusSquared ) ;
					""
				) ;

			// Else, reject guess and sample again
				RandomStandardNormal
		)
	)
)