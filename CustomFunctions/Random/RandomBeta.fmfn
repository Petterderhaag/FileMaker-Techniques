/*******************************************************************************
 * RandomBeta ( alpha ; beta )
 * Returns random values from a beta distribution.
 *
 * @parameter alpha: a number, alpha > 0
 * @parameter beta: a number, beta > 0
 *
 * @return A number between 0 and 1
 *
 * @history 2010-02-22 - Jeremy Bante <http://scr.im/fugue> - Created
 *
 * @see http://en.wikipedia.org/wiki/Beta_distribution
 ******************************************************************************/

Let ( [
	_guess = Random ;
	_mode = ( alpha - 1 ) / ( alpha + beta - 2 ) ;
	_densityAtMode =
		( _mode ^ ( alpha - 1 ) )
		* ( ( 1 - _mode ) ^ ( beta - 1 ) ) ;
	_rejectionThreshold =	// probability density at _guess
		( _guess ^ ( alpha - 1 ) )
		* ( ( 1 - _guess ) ^ ( beta - 1 ) )
		* _densityAtMode // maps function to [0,1] for max acceptance rate
] ;
	Case (
		alpha ≤ 0 or beta ≤ 0 ;	// parameters out of range
			"?" ;

		Random ≤ _rejectionThreshold ;
			_guess ;

		// Else, reject _guess and sample again
			RandomBeta ( alpha ; beta )
	)
)