/**
 * =====================================
 * #GetValues ( parameters )
 *
 * RETURNS:
 *		A ¶-delimited list of quoted values from the name-value pairs in
 *		parameters.
 *
 * PARAMETERS:
 *		parameters: A ¶-delimited list of name-value pairs generated by
 *		the # function.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-12-22 by Jeremy Bante <http://scr.im/jbante>.
 * =====================================
 */

Case (
	/* Step 0, set up */
	not $#.step;
		Let ( [
			$#.valueCount = ValueCount ( parameters );
			$#.step = 1
		];
			#GetValues ( parameters )
		);

	/* Step 1, evaluate values */
	$#.step = 1;
		Let ( [
			$#.i = $#.i + 1;

			~value = GetValue ( parameters ; $#.i );
			~delimiterPosition = Position ( ~value ; " = " ; 1 ; 1 );
			~value =
				Middle (
					~value;
					~delimiterPosition + 3;
					Length ( ~value ) - ~delimiterPosition - 3
				);

			$#.valueList = List ( $#.valueList ; ~value );
			$#.step =
				If ( $#.i ≥ $#.valueCount ; $#.step + 1 ; /* Else */ $#.step )
		];
			#GetValues ( parameters )
		);

	/* Step 2, clean up and return result */
	$#.step = 2;
		Let ( [
			~valueList = $#.valueList;

			// Purge variables
			$#.i = "";
			$#.valueList = "";
			$#.step = "";
			$#.valueCount = ""
		];
			~valueList
		)
)