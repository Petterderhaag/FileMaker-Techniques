/**
 * =====================================
 * #UnzipValues ( parameters )
 *
 * RETURNS:
 *		A return-delimited list of quoted values from the name-value pairs in
 *		parameters.
 *
 * PARAMETERS:
 *		parameters: A return-delimited list of name-value pairs generated by
 *		the # function.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		MODIFIED on 2012-05-08 by Jeremy Bante to simplify evaluation and to make the
 *		function a better inverse to the #Zip function.
 *		CREATED on 2011-12-22 by Jeremy Bante <http://scr.im/jbante>.
 * =====================================
 */

Let ( [
	$#.valueCount =
		If ( $#.valueCount ; $#.valueCount ; /* Else */ ValueCount ( parameters ) );
	$#.i = $#.i + 1;

	~value = GetValue ( parameters ; $#.i );
	~delimiterPosition = Position ( ~value ; " = " ; 1 ; 1 );
	~value =
		Middle (
			~value;
			~delimiterPosition + 3;
			Length ( ~value ) - ~delimiterPosition - 3
		)
];
	List (
		If ( ~delimiterPosition ; Evaluate ( ~value ) );
		If ( $#.i < $#.valueCount;
			#UnzipValues ( parameters );
		/* Else, clear variables */
			Let ( [ $#.valueCount = "" ; $#.i = "" ]; "" )
		)
	)
)