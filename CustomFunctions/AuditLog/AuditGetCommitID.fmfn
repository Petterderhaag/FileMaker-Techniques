/**
 * =====================================
 * AuditGetCommitID ( logData )
 *
 * RETURNS:
 *		The event ID (UUID) from the first (most recent) line of
 *		logData.
 *
 * PARAMETERS:
 *		logData: A series of log entries, as generated by the ChangeLog
 *			function.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-03-30 by Jeremy Bante <http://scr.im/jbantetsg>.
 * =====================================
 */

Let ( [
	~TAB = "	";
	~log = Substitute ( logData ; ~TAB ; "¶" );
	~isField = GetValue ( ~log ; 1 ) = "field:";
	~timestamp = GetAsTimestamp ( GetValue ( ~log ; 2 ) );
	~session = GetValue ( ~log ; 10 )
];
	If ( not ~isField;
		"1-2-" // version and reserved variant code
		& Right ( "000000000000" & Int ( ~timestamp ) ; 12 )
		& "-"
		& Left (
			Substitute (
				Mod ( ~timestamp ; 1 );
				"."; ""
			)
			& "0000000";
			7
		)
		& "-"
		& ~session
	)
)