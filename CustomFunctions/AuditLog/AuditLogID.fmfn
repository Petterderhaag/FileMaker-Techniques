/**
 * =====================================
 * AuditLogID ( logData )
 *
 * RETURNS:
 *		The event ID (UUID) from the first (most recent) line of
 *		logData.
 *
 * PARAMETERS:
 *		logData: A series of log entries, as generated by the ChangeLog
 *			function.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-03-30 by Jeremy Bante <http://scr.im/jbantetsg>.
 * =====================================
 */

Let ( [
	~TAB = "	";
	~log = Substitute ( logData ; ~TAB ; "¶" );
	~isEdit = GetValue ( ~log ; 1 ) = "Edit";
	~timestamp = GetValue ( ~log ; 2 );
	~session = GetValue ( ~log ; 10 )
];
	If ( not ~isEdit;
		"1-2-" // version and reserved variant code
		& Right ( "000000000000" & Int ( ~timestamp ) ; 12 )
		& "."
		& Left (
			Substitute (
				Mod ( ~timestamp ; 1 );
				"."; ""
			)
			& "0000000";
			7
		)
		& "-"
		& ~session
	)
)