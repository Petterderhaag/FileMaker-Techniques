/**
 * =====================================
 * AuditGetPriorValue ( logData )
 *
 * RETURNS:
 *		The prior ("from:") value from the first (most recent) line of logData
 *
 * PARAMETERS:
 *		logData: A series of log entries, as generated by the ChangeLog
 *			function.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-03-30 by Jeremy Bante <http://scr.im/jbante>.
 * =====================================
 */

Let ( [
	~TAB = "	";
	~log = Substitute ( logData ; ~TAB ; "¶" );
	~isField = GetValue ( ~log ; 1 ) = "field:";
	~value = GetValue ( ~log ; 6 )
];
	If ( ~isField;
		Substitute ( // un-escape field value from stored format
			Evaluate ( ~value );
			[ "\\t" ; ~TAB ];
			[ "\\\\" ; "\\" ]
		)
	)
)