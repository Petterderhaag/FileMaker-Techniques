/**
 * =====================================
 * IDTable ( tableName )
 *
 * RETURNS:
 *		FileMaker's internal ID for the current layout table occurrence.
 *
 * PARAMETERS:
 *		tableName
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-06-01 by Jeremy Bante <http://scr.im/jbante>.
 *		INSPIRED by custom functions by Fabrice Nordman.
 * =====================================
 */

 Let ( [
 	~fileName = Get ( FileName );
	~nameList = "¶" & TableNames ( ~fileName ) & "¶";
	~idList = TableIDs ( ~fileName );
	~value =
		Position (
			~nameList;
			"¶" & tableName & "¶";
			1;
			1
		);
	~value = PatternCount ( Left ( ~nameList ; ~value ) ; "¶" );
	~id = GetValue ( ~idList ; ~value )
];
	~id
)