/**
 * =====================================
 * IDCurrentField
 *
 * RETURNS:
 *		FileMaker's internal ID for the active field.
 *
 * PARAMETERS: none
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2011-06-01 by Jeremy Bante <http://scr.im/jbante>.
 *		INSPIRED by custom functions by Fabrice Nordman.
 * =====================================
 */

 Let ( [
 	~fieldName = GetFieldName ( GetField ( Get ( ActiveFieldName ) ) );
 	~fieldName = Substitute ( GetFieldName ( ~fieldName ) ; "::" ; "¶" );
 	~tableName = GetValue ( ~fieldName ; 1 );
 	~fieldName = GetValue ( ~fieldName ; 2 );
 	~fileName = Get ( FileName );
	~nameList = "¶" & FieldNames ( ~fileName ; ~tableName ) & "¶";
	~idList = FieldIDs ( ~fileName ; ~tableName );
	~value =
		Position (
			~nameList;
			"¶" & ~fieldName & "¶";
			1;
			1
		);
	~value = PatternCount ( Left ( ~nameList ; ~value ) ; "¶" );
	~id = GetValue ( ~idList ; ~value )
];
	~id
)