/*******************************************************************************
 * TriggersEnable
 * Sets global variables to indicate that the current script is no longer
 * suppressing triggers. This function will also turn off trigger suppression
 * altogether if there are no other scripts suppressing triggers. In order to be
 * suppressed, a script called via trigger should use the TriggersAreActive
 * function to decide whether or not to run.
 *
 * This function should *always* be used in pairs with the TriggersDisable
 * function.
 *
 * @global $$~DISABLETRIGGERS (deprecated)
 * @global $$~TRIGGERS_DISABLE
 * @global $$~TRIGGERS_SCRIPTS
 * @global $~triggers_disableKey
 *
 * @history 2010-12-12 - Jeremy Bante <http://scr.im/jbante> - Created
 * @history 2010-12-13 - Jeremy Bante <http://scr.im/jbante> - Using names
 * suggested at FileMakerStandards.org.
 * @history 2011-02-09 - Jeremy Bante <http://scr.im/jbante> - Adopting
 * different variable names.
 * @history 2013-03-25 - Jeremy Bante <http://scr.im/jbante> - Making the
 * function robust to sub-scripts that disabled triggers without re-enabling
 * them.
 * @history 2013-04-08 - Jeremy Bante <http://scr.im/jbante> - Adding older
 * variable names for backwards compatibility.
 *
 * @see http://filemakerstandards.org/display/bp/Suppressible+Triggered+Scripts
 ******************************************************************************/

If ( not IsEmpty ( $~triggers_disableKey ) ;
	Let ( [
		_scriptsLength = Length ( $$~TRIGGERS_SCRIPTS ) ;
		_scriptPosition =
			Position (
				$$~TRIGGERS_SCRIPTS ;
				Char ( 9 ) /* tab */ & $~triggers_disableKey & ¶ ;
				_scriptsLength ;
				-1
			) ;
		_breakPosition =
			If ( _scriptPosition > 0 ;
				Position ( $$~TRIGGERS_SCRIPTS ; ¶ ; _scriptPosition ; 1 ) ;
				/* Else */ 0
			) ;
		$$~TRIGGERS_SCRIPTS =
			Right ( $$~TRIGGERS_SCRIPTS ; _scriptsLength - _breakPosition ) ;
		$$~TRIGGERS_DISABLE =
			If ( not IsEmpty ( $$~TRIGGERS_SCRIPTS ) and $$~TRIGGERS_DISABLE ;
				True ;
				/* Else */ ""
			) ;
		$$~DISABLETRIGGERS = $$~TRIGGERS_DISABLE ;
		$~triggers_disableKey = ""
	] ;
		""
	)
)