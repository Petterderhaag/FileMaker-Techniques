/*******************************************************************************
 * TriggersDisable
 * Sets global variables to indicate that the current script is suppressing
 * triggers. This function must be called from the context of a script;
 * otherwise, it will not suppress triggers, and it will return False. In order
 * to be suppressed, a script called via trigger should use the
 * TriggersAreActive function to decide whether or not to run.
 *
 * @return True (1) if called from the context of a script, and therefore
 * triggers have been suppressed; False (0) otherwise.
 *
 * @global $$~DISABLETRIGGERS (deprecated)
 * @global $$~TRIGGERS_DISABLE
 * @global $$~TRIGGERS_SCRIPTS
 * @global $~triggers_disableKey
 *
 * @history 2010-12-12 - Jeremy Bante <http://scr.im/jbante> - Created
 * @history 2010-12-13 - Jeremy Bante <http://scr.im/jbante> - Using names
 * suggested at filemakerstandards.org.
 * @history 2011-02-09 - Jeremy Bante <http://scr.im/jbante> - Adopting
 * different variable names.
 * @history 2013-03-25 - Jeremy Bante <http://scr.im/jbante> - Making the
 * function robust to sub-scripts that disabled triggers without re-enabling
 * them.
 * @history 2013-04-08 - Jeremy Bante <http://scr.im/jbante> - Adding older
 * variable names for backwards compatibility.
 *
 * @see http://filemakerstandards.org/display/bp/Suppressible+Triggered+Scripts
 ******************************************************************************/

Let ( [
	_scriptName = Get ( ScriptName ) ;
	_scriptIsRunning = not IsEmpty ( Get ( ScriptName ) ) ;
	_disableIsRunning = not IsEmpty ( $~triggers_disableKey ) ;
	$~triggers_disableKey =
		Case (
			_disableIsRunning ; $~triggers_disableKey ;
			_scriptIsRunning ; Get ( UUID )
		) ;
	$$~TRIGGERS_SCRIPTS =
		If ( _scriptIsRunning and not _disableIsRunning ;
			_scriptName
			& Char ( 9 )	// tab
			& $~triggers_disableKey
			& ¶
			& $$~TRIGGERS_SCRIPTS ;
		/* Else */
			$$~TRIGGERS_SCRIPTS
		) ;
	$$~TRIGGERS_DISABLE =
		If ( _scriptIsRunning ; True ; /* Else */ $$~TRIGGERS_DISABLE ) ;
	$$~DISABLETRIGGERS = $$~TRIGGERS_DISABLE
] ;
	_scriptIsRunning
)