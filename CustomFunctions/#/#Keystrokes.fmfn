/**
 * =====================================
 * #TriggerKeys
 *
 * PURPOSE:
 *		Creates a series of name-value pairs based on the current active and
 *		trigger keystrokes.
 *
 * RETURNS:
 *		A series of name-value pairs generated by the # function.
 *
 * PARAMETERS: none
 *
 * DEPENDENCIES:
 *		# ( name ; value )
 *
 * HISTORY:
 *		CREATED on 2010-09-06 by Jeremy Bante <http://scr.im/jbantetsg>.
 *		INSPIRED by a function created by Marx Martin of The Support Group.
 * =====================================
 */

 Let ( [
	~activeModifierKeys = Get ( ActiveModifierKeys );
	~triggerModifierKeys = 	Get ( TriggerModifierKeys );

	~shift =
		Mod ( ~activeModifierKeys ; 2 )
		or Mod ( ~triggerModifierKeys ; 2 );
	~capsLock =
		Mod ( Div ( ~activeModifierKeys ; 2 ) ; 2 )
		or Mod ( Div ( ~triggerModifierKeys ; 2 ) ; 2 );
	~control =
		Mod ( Div ( ~activeModifierKeys ; 4 ) ; 2 )
		or Mod ( Div ( ~triggerModifierKeys ; 4 ) ; 2 );
	~option =
		Mod ( Div ( ~activeModifierKeys ; 8 ) ; 2 )
		or Mod ( Div ( ~triggerModifierKeys ; 8 ) ; 2 );
	~command =
		Mod ( Div ( ~activeModifierKeys ; 4 ) ; 2 )
		or Mod ( Div ( ~triggerModifierKeys ; 4 ) ; 2 );

	~keyCode = Code ( Get ( TriggerKeystroke ) )
];
	If ( ~keyCode ; # ( "triggerKeyCode" ; ~keycode ) )
	& If ( ~shift ; # ( "keyShift" ; ~shift ) )
	& If ( ~capsLock ; # ( "keyCapsLock" ; ~capsLock ) )
	& If ( ~control ; # ( "keyControl" ; ~control ) )
	& If ( ~option ; # ( "keyOption" ; ~option ) )
	& If ( ~command ; # ( "keyCommand" ; ~command ) )
)

/*
 * Shift = 1 (2^0)
 * Caps Lock = 2 (2^1)
 * Ctrl (Windows) and Control (Mac OS) = 4 (2^2)
 * Alt (Windows) and Option (Mac OS) = 8 (2^3)
 * Command (Mac OS) = 16 (2^4)
 *
 * Combined by addition, ex: Shift + Command = 1 + 16 = 17
 * 2^4 + 2^3 + 2^2 + 2^1 + 2^0
 * 1 = Shift
 * 2 = Caps Lock
 * 3 = 1 + 2 = Shift + CapsLock
 * 4 = Control
 * 5 = 4 + 1 = Control + Shift
 * 12 = Control + Alt
 * 9 = Option + Shift
 */