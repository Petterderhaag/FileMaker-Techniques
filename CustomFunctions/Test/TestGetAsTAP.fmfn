/**
 * =====================================
 * TestGetAsTAP ( testResult )
 *
 * RETURNS:
 *		The information from a series of name-value pairs from the Test function
 *		structured according to the Test Anything Protocol (TAP), a human-readable test
 *		log format.
 *
 * PARAMETERS:
 *		testResult
 *
 * DEPENDENCIES:
 *		#Get ( parameters ; value )
 *
 * HISTORY:
 *		CREATED on 2012-05-31 by Jeremy Bante <http://scr.im/jbante>.
 *
 * REFERENCES:
 *		http://testanything.org/
 * =====================================
 */

Let ( [
	~pass = #Get ( testResult ; "pass" );
	~diagnostic = #Get ( testResult ; "diagnostic" );
	~log =
		TextStyleAdd ( If ( ~pass ; "ok" ; /* Else */ "not ok" ) ; Bold )
		& "	-	"
		& Substitute (
			#Get ( testResult ; "description" );
			[ ¶ ; " " ];
			[ "	" ; " " ]
		)
		& "	# TIME "
		& #Get ( testResult ; "timestamp" )
];
	If ( ~pass;
		~log;
	/* Else */
		TextColor ( ~log ; RGB ( 200 ; 0 ; 0 ) )	// Red
		& If ( not IsEmpty ( ~diagnostic );
			"¶# "
			& Substitute ( ~diagnostic ; ¶ ; "¶# " )
		)
	)
)