/*******************************************************************************
 * TextMaskPhone ( phoneNumber )
 * Formats a string of text as a North America phone number.
 *
 * @parameter phoneNumber: The phone number to format
 *
 * @return The formatted phone number text
 *
 * @uses TextMask ( text ; prototype )
 *
 * @history 2012-12-05 - Jeremy Bante <http://scr.im/fugue> - Created
 * @history 2014-12-03 - Jeremy Bante <http://scr.im/fugue> - Updating
 * formatting and reorganizing logic
 ******************************************************************************/

Let ( [
	_alphanumeric = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ" ;
	_text = Filter ( Upper ( phoneNumber ) ; _alphanumeric ) ;
	_mask =
		Case (
			Left ( _text ; 1 ) = "1" ;	"#-###-###-#### ext. *" ;
			Length ( _text ) > 7 ;	"###-###-#### ext. *" ;
			/* Else */	"###-####"
		)
] ;
	TextMask ( _text ; _mask )
)