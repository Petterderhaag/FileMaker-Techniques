/*******************************************************************************
 * Orientation ( x1 ; y1 ; x2 ; y2 ; x3 ; y3 )
 * Determines whether a sequence of 3 points in the cartesian plane is oriented
 * clockwise, counter-clockwise, or co-linear.
 *
 * @parameter x1: x coordinate of the first point in the sequence
 * @parameter y1: y coordinate of the first point in the sequence
 * @parameter x2
 * @parameter y2
 * @parameter x3
 * @parameter y3
 *
 * @return +1 if the sequence is oriented counter-clockwise, -1 if the sequence
 * is clockwise, and 0 if the points are co-linear.
 *
 * @history 2016-08-24 - Jeremy Bante <http://scr.im/fugue> - Created
 * @history 2016-08-25 - Jeremy Bante <http://scr.im/fugue> - Improving
 * tolerance for points that are very close together.
 *
 * @see http://algs4.cs.princeton.edu/91primitives/
 ******************************************************************************/

Let ( [
	_a = x2 - x1 ;
	_b = y3 - y1 ;
	_abSign = Sign ( _a ) * Sign ( _b ) ;
	_c = x3 - x1 ;
	_d = y2 - y1 ;
	_cdSign = Sign ( _c ) * Sign ( _d ) ;
	_min = Min ( Abs ( _a ) ; Abs ( _b ) ; Abs ( _c ) ; Abs ( _d ) ) ;
	_pointsAreClose = _min < .00001 and _min > 0 ;
	_α =
		If ( _pointsAreClose ;
			Log ( Abs ( _a ) ) + Log ( Abs ( _b ) ) ;
			/* Else */ _a * _b
		) ;
	_β =
		If ( _pointsAreClose ;
			Log ( Abs ( _c ) ) + Log ( Abs ( _d ) ) ;
			/* Else */ _c * _d
		)
] ;
	Case (
		not _pointsAreClose ;	Sign ( _α - _β ) ;
		_abSign > _cdSign ;	1 ;
		_abSign < _cdSign ;	-1 ;
		_abSign > 0 and _α > _β ;	1 ;
		_abSign > 0 and _α < _β ;	-1 ;
		_abSign < 0 and _α > _β ;	-1 ;
		_abSign < 0 and _α < _β ;	1 ;
		/* Else */	0
	)
)