/**
 * =====================================
 * QuantileChebyshev ( mean ; standardDeviation ; p )
 *
 * RETURNS:
 *		The value from a probability distribution such that other values sampled
 *		from the same distribution fall below that value with p probability,
 *		according to Chebyshev's inequality.
 *
 * PARAMETERS:
 *		mean
 *		standardDeviation
 *		p: The cumulative probability at the desired quantile. 0 < p < 1.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 *		CREATED on 2013-03-12 by Jeremy Bante <http://scr.im/jbante>.
 *
 * REFERENCES:
 *		http://en.wikipedia.org/wiki/Chebyshev%27s_inequality
 * =====================================
 */

Let ( [
	~q = If ( p > .5 ; 1 - p ; /* Else */ p ) ;
	~deviation = Sqrt ( .5 / ~q ) * standardDeviation	// z score * stdDev
] ;
	Case (
		p ≤ 0 or p ≥ 1 ; "?" ;	// out of bounds
		p < .5 ; mean - ~deviation ;
		/* Else, p ≥ .5 */ mean + ~deviation
	)
)