/*******************************************************************************
 * HexFromColor ( color )
 * Converts a FileMaker color value (values from the RGB ( red ; green ; blue )
 * function) to a hexadecimal color value suitable for use in Web pages,
 * including the contents of webviewer objects.
 *
 * @parameter color: FileMaker's numeric encoding of a 96-bit color value
 *
 * @return A text string of the form #hhhhhh, where h is a hexadecimal digit
 *
 * @history 2011-03-01 - Jeremy Bante <http://scr.im/fugue> - Created
 ******************************************************************************/

Let ( [
	_hex = "0123456789abcdef"
] ;
	"#"
	& Middle ( _hex ; Mod ( Div ( color ; 1048576 ) ; 16 ) + 1 ; 1 )
	& Middle ( _hex ; Mod ( Div ( color ; 65536 ) ; 16 ) + 1 ; 1 )
	& Middle ( _hex ; Mod ( Div ( color ; 4096 ) ; 16 ) + 1 ; 1 )
	& Middle ( _hex ; Mod ( Div ( color ; 256 ) ; 16 ) + 1 ; 1 )
	& Middle ( _hex ; Mod ( Div ( color ; 16 ) ; 16 ) + 1 ; 1 )
	& Middle ( _hex ; Mod ( color ; 16 ) + 1 ; 1 )
)