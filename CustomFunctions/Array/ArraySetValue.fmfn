/*******************************************************************************
 * ArraySetValue ( array ; index ; data )
 * Sets a value at a specified position in an arbitrary-dimensional array. This
 * will replace any existing contents of the passed array in that cell.
 *
 * @parameter array: An quoted value return-delimited list array.
 * @parameter index: The index for the cell from which to extract a value. To
 * address values from multi-dimentional arrays, separate the index for each
 * dimension with ¶, or list the indices with the List function.
 * @parameter data: The data to set at the specified index in the array
 *
 * @return An array containing the data value at the specified index, in
 * addition to any other values in array.
 *
 * @history 2011-02-11 - Jeremy Bante <http://scr.im/jbante> - Created
 ******************************************************************************/

Let ( [
	_index = GetAsNumber ( GetValue ( index ; 1 ) ) ;
	_indexCount = ValueCount ( index ) ;
	_arrayLength = ValueCount ( array ) ;

	// Array may need to be padded with values to be big enough for _index
	_short = Max ( _index - _arrayLength - 1 ; 0 ) ;
	$~asv.pad =
		Case (
			not _short
			and _indexCount ≤ 1 ;
				"" ;	// purge variable

			_short ≤ ValueCount ( $~asv.pad ) ;
				$~asv.pad ;

			/* Else */
				$~asv.pad
				& $~asv.pad
				& "¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶¶"
		) ;

	// Values to repeat from before and after _index
	_leftArray = LeftValues ( array ; _index - 1 ) ;
	_rightArray = RightValues ( array ; _arrayLength - _index )
] ;
	Case (
		_short ;	// array needs to be padded
			ArraySetValue (
				array & Left ( $~asv.pad ; _short ) ;
				index ;
				data
			) ;

		_indexCount ≤ 1 ;
			_leftArray
			& Quote ( data ) & "¶"
			& _rightArray ;

		_indexCount > 1 ;
			_leftArray
			& Quote (
				ArraySetValue (
					Evaluate ( GetValue ( array ; _index ) ) ;
					RightValues ( index ; _indexCount - 1 ) ;
					data
				)
			)
			& "¶"
			& _rightArray ;
	)
)