/*******************************************************************************
 * ArrayQuoteList ( valueList )
 * Quotes the values in a ¶-delimited value list individually.
 *
 * @parameter valueList: A ¶-delimited list of values to quote
 *
 * @return The quoted values from valueList, in the original order
 *
 * @history 2011-01-25 - Jeremy Bante <http://scr.im/fugue> - Created
 ******************************************************************************/

Case (
	/*Step 1, initialize variables*/
	not $~step;
	Let ( [
		$~i = $~i + 1;
		$~value = GetValue ( valueList ; $~i );
		$~quotedValues = List ( $~quotedValues ; Quote ( $~value ) );
		$~step = If ( $~i = ValueCount ( valueList ) ; 1 )
	];
		ArrayQuoteList ( valueList )
	);

	/*Step 1, clean-up and return value*/
	$~step = 1;
	Let ( [
		_quotedValues = $~quotedValues;

		//purge variables
		$~i = "";
		$~step = "";
		$~quotedValues = "";
		$~value = ""
	];
		_quotedValues
	)
)