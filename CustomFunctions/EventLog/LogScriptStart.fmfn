/**
 * =====================================
 * LogScriptStart
 *
 * PURPOSE:
 *		Creates a new EventLog record for the current script. When used in
 *		combination with the other Log* functions, this function can be used to
 *		log the precise run times of various scripted processes.
 *
 * RETURNS: "" (Null)
 *
 * PARAMETERS: none
 *
 * DEPENDENCIES:
 *		GaiaPlugin (plug-in)
 *		GetFieldNameOnly (custom function)
 *		ScriptID (custom function)
 *		SQLQuote (custom function)
 *
 * VARIABLES:
 *		$~eventLog.id_process
 *
 * HISTORY:
 *		CREATED on 2012-01-17 by Jeremy Bante <http://scr.im/jbante>.
 *		MODIFIED on 2012-04-24 by Jeremy Bante <http://scr.im/jbante> to use the
 *		GaiaPlugin.
 * =====================================
 */

Let ( [
	$~eventLog.id_process = GetAsNumber ( UUIDTimeNIC )
];
	GaiaExecuteSQL (
		"INSERT INTO EventLog¶( "
			& GetFieldNameOnly ( EventLog::id_Process )
			& ",	" & GetFieldNameOnly ( EventLog::id_Script )
			& ",	" & GetFieldNameOnly ( EventLog::fileName )
			& ",	" & GetFieldNameOnly ( EventLog::event )
			& ",	" & GetFieldNameOnly ( EventLog::startNanoseconds )
		& " )¶VALUES¶( "
			& $~eventLog.id_process
			& ",	" & ScriptID ( Get ( FileName ) ; Get ( ScriptName ) )
			& ",	" & SQLQuote ( Get ( FileName ) )
			& ",	" & SQLQuote ( "script" )
			& ",	" & GaiaNanoTime
		& " )";
		"" ; ""	// column and row separators
	)
)