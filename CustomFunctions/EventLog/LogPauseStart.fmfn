/**
 * =====================================
 * LogPauseStart
 *
 * PURPOSE:
 *		Creates a new pause EventLog record for the current script so that user
 *		pauses are not included in measures of machine performance. When used in
 *		combination with the other Log* functions, this function can be used to
 *		log the precise run times of various scripted processes.
 *
 * RETURNS: "" (Null)
 *
 * PARAMETERS: none
 *
 * DEPENDENCIES:
 *		GaiaPlugin (plug-in)
 *		GetFieldNameOnly (custom function)
 *		SQLQuote (custom function)
 *
 * VARIABLES:
 *		$~eventLog.id_process
 *		$~eventLog.id_pause
 *
 * HISTORY:
 *		CREATED on 2012-01-17 by Jeremy Bante <http://scr.im/jbante>.
 *		MODIFIED on 2012-04-24 by Jeremy Bante <http://scr.im/jbante> to use the
 *		GaiaPlugin.
 * =====================================
 */

Let ( [
	$~eventLog.id_pause = GetAsNumber ( UUIDTimeNIC )
];
	GaiaExecuteSQL (
		"INSERT INTO EventLog¶( "
			& GetFieldNameOnly ( EventLog::id_Pause )
			& ",	" & GetFieldNameOnly ( EventLog::id_Process )
			& ",	" & GetFieldNameOnly ( EventLog::event )
			& ",	" & GetFieldNameOnly ( EventLog::startNanoseconds )
		& " )¶VALUES¶( "
			& $~eventLog.id_pause
			& ",	" & $~eventLog.id_process
			& ",	" & SQLQuote ( "pause" )
			& ",	" & GaiaNanoTime
		& " )";
		"" ; ""	// column and row separators
	)
)